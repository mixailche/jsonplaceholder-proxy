plugins {
    id 'java'
    id 'org.jooq.jooq-codegen-gradle' version '3.19.0'
    id 'org.springframework.boot' version '3.2.3'
    id 'org.liquibase.gradle' version '2.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
}

java {
    sourceCompatibility = '19'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'commons-codec:commons-codec:1.16.1'
    implementation 'com.auth0:java-jwt:4.4.0'

    implementation 'org.jooq:jooq:3.19.0'
    runtimeOnly 'org.postgresql:postgresql:42.6.1'
    jooqCodegen 'org.postgresql:postgresql:42.6.1'

    liquibaseRuntime 'org.liquibase:liquibase-core:4.26.0'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.1.1'
    liquibaseRuntime 'info.picocli:picocli:4.7.5'
    liquibaseRuntime 'org.yaml:snakeyaml:2.2'
    liquibaseRuntime 'org.postgresql:postgresql:42.6.1'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

liquibase {
    activities {
        main {
            changelogFile 'src/main/resources/db/db-changelog-master.yaml'
            url 'jdbc:postgresql://localhost:5432/json-proxy'
            username 'admin'
            password 'admin'
        }
    }
}

jooq {
    configuration {

        jdbc {
            url = 'jdbc:postgresql://localhost:5432/json-proxy'
            user = 'admin'
            password = 'admin'
        }

        generator {
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                includes = '.*'
                excludes = ''
                inputSchema = 'public'
            }

            generate {}
            target {
                packageName = 'com.jsonplaceholder.proxy.jooq'
                directory = projectDir.toString() + '/src/main/java'
            }
        }

    }
}
